Prepare_Game_Screen
            jsr Track_Profile_Calc_and_Draw
            jsr Print_Distance
            jsr Clear_Playscreen_Buffer
            ldy #$01
 _loop      lda (Track_Location_Address),y
            iny
            beq _skip
            cmp #$FF
            beq _skip           ;end, skip Signal update
            lsr a
            bcs _loop           ;more 7f, skip
            asl a
            cmp #$0C            
            bcc _loop           ;less than $0c
            cmp #$13            
            bcs _loop           ;More than 13 
            ldy #$FF            ;here we have 0e,10,12
 _loop1     iny
            cmp Signal_Coordinate_Table,y
            bne _loop1
            tya
            adc #<Signal_Coordinate_Table
            tax
            lda #$00
            adc #>Signal_Coordinate_Table
            tay
            jsr Draw_2d_Lines
 _skip       ; Copy Signal from Buffer to Bitmap
             ; Better than original
            ldx #$3f
 -          lda Gfx_Buffer,x
            sta Bitmap+$12c8,x
            lda Gfx_Buffer+$a0,x
            sta Bitmap+$12c8+320,x
            lda Gfx_Buffer+2*$a0,x
            sta Bitmap+$12c8+2*320,x
            lda Gfx_Buffer+3*$a0,x
            sta Bitmap+$12c8+3*320,x
            lda Gfx_Buffer+4*$a0,x
            sta Bitmap+$12c8+4*320,x
            lda Gfx_Buffer+5*$a0,x
            sta Bitmap+$12c8+5*320,x
            lda Gfx_Buffer+6*$a0,x
            sta Bitmap+$12c8+6*320,x
            dex
            bpl -
            rts