@echo off &Cls

Rem define and create the Top level folder for Prompts to be stored.
Set "TargetFolder=%Userprofile%\Pictures\Midjourney"
Rem change the above definition to a different directory if desired.

REM Storage tree
REM %SystemRoot% IE: C:\
REM  - Users
REM    - UserName
REM      - Pictures
REM        - Midjoureny
REM          - Prompt_Text

MD "%TargetFolder%" 2>nul

REM Script requires calling environment to NOT have EnableDelayedExpanion active.
	If "!!" == "" Exit /B 1

REM ensure correct environment for scripts execution
	Setlocal EnableExtensions DisableDelayedExpansion

(Set \n=^^^

%= Newline var \n for multi-line macro definition - Do not modify. =%)

REM supports all ASCII printable characters in range 32 ~ 126
REM NOTE: CR & LF are not printable characters.

Set Reverse=For %%n in (1 2)Do if %%n==2 (%\n: String Reverse Macro by T3RRY =%
	For /F "tokens=1,2 Delims=, " %%E in ("!Reverse_Args!")Do (%\n: Prep string for Parsing =%
		Set "TempString=!%%~E!"%\n%
		If Defined TempString (%\n%
			Set ^^^"TempString=!TempString:"=``'``!"%\n%
			Set "TempString=!TempString:^=^^!"%\n%
			Set "TempString=!TempString:&=^&!"%\n%
			Set "TempString=!TempString:|=^|!"%\n%
			Set "TempString=!TempString:<=^<!"%\n%
			Set "TempString=!TempString:>=^>!"%\n%
			Set "TempString=!TempString:^=^^^!"%\n%
			Set "NewString="%\n%
			Set "Pos[#]=0"%\n: Split string by character and rebuild; handle detection and escaping of poison characters '!' '^' and '"' =%
			For /f "usebackq Delims=" %%G in (`%Systemroot%\System32\cmd.exe /u /c ^"Echo(!TempString!^"^^^|%Systemroot%\System32\find.exe /v "[false_match_%~n0]"^^^|%Systemroot%\System32\findstr.exe "^^"`)Do (%\n%
				Set "Char="%\n%
				If "^%%~G" == "^^" (%\n: Character is a Caret =%
					Set "Char=^"%\n%
				)Else (%\n%
					Set "Char="%\n%
					If "^^^%%~G" == "^^^!" (%\n: Character is an Exclamation mark =%
						Set "Char=^^^%%~G"%\n%
					) Else (%\n%
						Set Char=^^%%~G%\n%
					)%\n%
				)%\n%
				Set "NewString=!Char!!NewString!"%\n%
			)%\n%
		)%\n: end string rebuild; Enact Escaping restorations =%
		Set "NewString=!NewString:^^^=^^!"%\n%
		Set "NewString=!NewString:^^={TwoCarets}!"%\n%
		Set "NewString=!NewString:&^^=&^!"%\n%
		Set "NewString=!NewString:|^^=|^!"%\n%
		Set "NewString=!NewString:<^^=<^!"%\n%
		Set "%%~F=!NewString:>^^=>^!"%\n%
		Set "NewString=!NewString:^=!"%\n%
		Set "NewString=!NewString:{TwoCarets}=^!"%\n%
		Set ^^^"NewString=!NewString:``'``="!"%\n%
		Set "Return=$Return"%\n%
		If Defined TempString (%\n%
			Set "Return=%%F"%\n%
		)Else Set "NewString="%\n%
	)%\n: Rebuild complete; return across endlocal =%
	For /f "tokens=1* Delims=`" %%R in ("!Return!`!NewString!")Do (%\n%
		Endlocal ^& If /I not "%%R" == "Reverse" Set "%%R=%%S"%\n%
	)%\n%
)Else Setlocal EnableDelayedExpansion ^& Set Reverse_Args=

REM get argument for processing
	Set "InputFile="
	Set "InputFile=%~n1"

	If not defined InputFile (
		Echo(%0 "file"
		Endlocal & Exit /b 1
	)

	Echo(Processing "%~n1"

REM Reverse the string to facilitate removal of job ID and file extension
	%Reverse% InputFile String

REM remove job ID and file extension
	Set "string=%string:*_=%"

REM Restore string orientation.
	%Reverse% String InputFile
	
	Echo(Moving all files with prefix "%InputFile%"
	MD "%TargetFolder%\%InputFile%" 2> nul

	Move  "%InputFile%*.*" "%TargetFolder%\%InputFile%"
	Echo(Done
Pause

Endlocal & Exit /b 0
