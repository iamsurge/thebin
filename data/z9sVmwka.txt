(* 

Version 5.0.11
11/28/2022

QLab 5 EOS MIDI Cue Range Generator

This script will generate a range of cues for ETC EOS, making use of the new libraries in QLab 5.

Changes:
v5.0.11 Adapted the script to account for the added functionality of EOS user numbers in QLab 5.0.9

Written by Chase Elison
chase@chaseelison.com

Tested with QLab v5.0.11 on macOS Monterey 12.6

*)

tell application id "com.figure53.QLab.5" to tell front workspace
	--
	-- BEGIN QLab VERSION CHECK SNIPPET
	-- Chase Elison 11/28/2022
	--
	set versionOfQLab to get version of application "QLab"
	set text item delimiters of AppleScript to "."
	set versionNumber to text items of versionOfQLab
	set isVersion5 to (item 1 of versionNumber is "5") as boolean
	set isNewVersionWithUser to false as boolean
	-- By default, it will not add user data
	if (item 2 of versionNumber is "0") and (item 3 of versionNumber as integer ≥ 9) then
		-- Is build .0.9 or higher, therefore will add user info in cues.
		set isNewVersionWithUser to true
	else if item 2 of versionNumber as integer ≥ 1 then
		-- Is greater than build .1.x, it will be presumed that newer versions will follow the same format
		-- At time of writing this, 11/28/2022, QLab 5.0.10 is newest version.
		set isNewVersionWithUser to true
	end if
	--
	-- END QLab VERSION CHECK SNIPPET
	-- Variable isNewVersionWithuser will indicate whether the cues should be generated with user data or not.
	--
	display dialog "First Cue Number?" default answer "1" with title "QLab 5 EOS Cue Range Generator" with icon 1 buttons {"OK", "Cancel"} default button "OK"
	if button returned of result = "cancel" then
		return
	else
		set firstCueNumber to (text returned of result) as integer
	end if
	
	display dialog "Last Cue Number?" default answer "1" with title "QLab 5 EOS Cue Range Generator" with icon 1 buttons {"OK", "Cancel"} default button "OK"
	if button returned of result = "cancel" then
		return
	else
		set lastCueNumber to (text returned of result) as integer
	end if
	
	display dialog "Increment?" default answer "1" with title "QLab 5 EOS Cue Range Generator" with icon 1 buttons {"OK", "Cancel"} default button "OK"
	set incrementResult to result
	if button returned of incrementResult = "cancel" then
		return
	else
		if text returned of incrementResult is "0" then
			display dialog "You certified silly goose, it can't be 0" with title "Silly goose" with icon 1 buttons {"I accept my fate as a silly goose"} default button "I accept my fate as a silly goose"
			return
		else
			set increment to (text returned of incrementResult)
		end if
	end if
	
	display dialog "Specific cue list?" with title "QLab 5 EOS Cue Range Generator" with icon 1 buttons {"Yes", "No"} default button "Yes"
	if button returned of result = "yes" then
		display dialog "Which list?" default answer "1" with title "QLab 5 EOS Cue Range Generator" with icon 1 buttons {"OK", "Cancel"} default button "OK"
		set cueListResult to result
		if button returned of cueListResult = "cancel" then
			return
		else
			set eosSpecifyCueList to true
			set eosCueList to (text returned of cueListResult)
		end if
	else
		set eosSpecifyCueList to false
		set eosCueList to "0"
	end if
	
	display dialog "Prefix? For example, Q, LQ, Light Cue..." default answer "Q" with title "QLab 5 EOS Cue Range Generator" with icon 1 buttons {"OK", "Cancel"} default button "OK"
	if button returned of result = "cancel" then
		return
	else
		set cueNamePrefix to (text returned of result) as string
	end if
	
	display dialog "EOS - MIDI RX Device ID?" with icon 1 default answer "0"
	try
		set qlabMidiDeviceID to (text returned of result) as integer
		if (qlabMidiDeviceID < 0) or (qlabMidiDeviceID > 127) then
			display dialog "ERROR - must be a number between 0 and 127" with icon 1 buttons {"OK"} default button 1
			return
		end if
	on error
		display dialog "ERROR - must be a number between 0 and 127" with icon 1 buttons {"OK"} default button 1
		return
	end try
	
	
	set currentCueNumber to firstCueNumber
	repeat while (currentCueNumber ≤ lastCueNumber) and (currentCueNumber ≥ firstCueNumber) --in case a silly goose tries a negative increment
		make type "MIDI"
		set qlabNewCue to last item of (selected as list)
		--Following if block was added because it kept giving values like "10.0" instead of "10" and it appears to work for now. 
		if (currentCueNumber as integer) is currentCueNumber then
			set eosCueNumber to currentCueNumber as integer as text
		else
			set eosCueNumber to currentCueNumber as text
		end if
		--If spec'ing a list and it's not 0 or 1, Q name should be list/cue. Otherwise, just cue.
		if eosSpecifyCueList and eosCueList is not in {"1", "0"} then
			set cueDisplayNumber to eosCueList & "/" & eosCueNumber
		else
			set cueDisplayNumber to eosCueNumber
		end if
		set q name of qlabNewCue to cueNamePrefix & cueDisplayNumber
		set q number of qlabNewCue to ""
		
		set message type of qlabNewCue to msc
		set command format of qlabNewCue to 1 -- Lighting (General)
		set command number of qlabNewCue to 1 -- GO
		set deviceID of qlabNewCue to qlabMidiDeviceID
		set q_number of qlabNewCue to eosCueNumber
		if eosSpecifyCueList then
			set q_list of qlabNewCue to eosCueList
		end if
		
		set currentCueNumber to currentCueNumber + increment
	end repeat
end tell