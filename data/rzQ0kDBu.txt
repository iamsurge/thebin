@echo off
REM Reverse macro by T3RRY. 28/11/2022
REM Handles any string containing ASCII printable characters in the range 32 ~ 126
REM With the exception of strings containing unbalanced Doublequotes.

	Set "$Example=/?! ^&f><oo%%.^|bar ^ ""` &^^ & | *((:~=!)^^ ^^^"

REM requires calling environment to NOT have EnableDelayedExpanion active.
	If "!!" == "" Exit /B 1

(Set \n=^^^

%= Newline var \n for multi-line macro definition - Do not modify. =%)

REM Macro Function fail case: strings containing unbalanced doublequotes.
Set DQ=^"
Set Reverse=For %%n in (1 2)Do if %%n==2 (%\n%
	For /F "tokens=1,2 Delims=, " %%E in ("!Reverse_Args!")Do (%\n: Prep string for Parsing =%
		Set "TempString=!%%~E!"%\n%
		Set "TempString=!TempString:^=^^!"%\n%
		Set "TempString=!TempString:&=^&!"%\n%
		Set "TempString=!TempString:|=^|!"%\n%
		Set "TempString=!TempString:<=^<!"%\n%
		Set "TempString=!TempString:>=^>!"%\n%
		Set "TempString=!TempString:^=^^^!"%\n%
		Set "NewString="%\n%
		Set "Pos[#]=0"%\n: Split string by character and rebuild; handle detection and escaping of poison characters '!' '^' and '"' =%
		For /f "usebackq Delims=" %%G in (`%Systemroot%\System32\cmd.exe /u /c ^"Echo(!TempString!^"^^^|%Systemroot%\System32\find.exe /v "[false_match_%~n0]"^^^|%Systemroot%\System32\findstr.exe "^^"`)Do (%\n%
			Set /A "Pos[#]+=1"%\n%
			If [^^%%~G] == [^^^^] (%\n: Character is a Caret =%
				Set "Char@!Pos[#]!=^^"%\n%
				Set "Last_Char=Caret"%\n%
			)Else (%\n%
				Set "Last_Char="%\n%
				Set "Char@!Pos[#]!="%\n%
			)%\n%
			If "^^^%%~G" == "^^^!" (%\n: Character is an Exclamation mark =%
				Set Char@!Pos[#]!=^^^^^^%%~G%\n%
			) Else (%\n%
				If not defined Char@!Pos[#]! (Set Char@!Pos[#]!=^^%%~G)%\n%
			)%\n%
			If not defined Char@!Pos[#]! (%\n: Character is a Double Qquote =%
				If not defined Last_Char (Set Char@!Pos[#]!=!DQ!)%\n%
			)%\n%
			For %%v in ("!Pos[#]!")Do (%\n%
				Set "NewString=!Char@%%~v!!NewString!"%\n%
			)%\n%
		)%\n: end string rebuild; Enact Escaping restorations =%
		Set "NewString=!NewString:^^^=^^!"%\n%
		Set "NewString=!NewString:^^={TwoCarets}!"%\n%
		Set "NewString=!NewString:&^^=&^!"%\n%
		Set "NewString=!NewString:|^^=|^!"%\n%
		Set "NewString=!NewString:<^^=<^!"%\n%
		Set "%%~F=!NewString:>^^=>^!"%\n%
		Set "NewString=!NewString:^=!"%\n%
		Set "NewString=!NewString:{TwoCarets}=^!"%\n%
		Set "Return=%%~F"%\n%
	)%\n: Rebuild complete; return across endlocal =%
	For /f "tokens=1* Delims=`" %%R in ("!Return!`!NewString!")Do (%\n%
		Endlocal ^& Set "%%R=%%S"%\n%
	)%\n%
)Else Setlocal EnableDelayedExpansion ^& Set Reverse_Args=
CLS
Rem Examples
	%Reverse% $Example $Result1
	%Reverse% $Result1 $Result2
	Set $

Pause
Goto:Eof
