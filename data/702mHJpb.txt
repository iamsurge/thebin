===============================
BDFD Ticket system with buttons in the ticket
Made by ShadowsNemesis
===============================
variable - value
******************
ticketnumber - 0
msgid - 

===============================
Code 1) (prefix)ticket
BDscript2
*********************
$nomention
$title[TICKET]
$description[To create a ticket, click ðŸ“©]
$color[1700fb]
$addButton[no;newticket;ðŸ“©;primary;no;]
$deletecommand
$onlyAdmin[You cannot Use this command!]
$authorIcon[$serverIcon]
$author[$serverName[$guildID]]

===============================
Code 2) $onInteraction
BDscript2
*********************
$onlyIf[$checkContains[$customID;newticket;createticket]==true;]
$nomention
$if[$customID==newticket]
$newModal[createticket;Ticket]
$addTextInput[reason;paragraph;reason;10;4000;yes;;breifly explain]
$endif

$if[$customID==createticket]
$if[$channelExists[$findChannel[Ticket]]==true]
$createChannel[$sum[$getServerVar[ticketnumber];1]-ticket;text;$findChannel[Ticket]]

$setServerVar[ticketnumber;$sum[$getServerVar[ticketnumber];1]]

$elseif[$channelExists[$findChannel[Ticket]]!=true]
$createChannel[Ticket;category]

$editChannelPerms[$findChannel[Ticket];$findRole[@everyone];-readmessages;-sendmessages;-readmessagehistory]

$createChannel[$sum[$getServerVar[ticketnumber];1]-ticket;text;$findChannel[Ticket]]

$setServerVar[ticketnumber;$sum[$getServerVar[ticketnumber];1]]
$endif

$replyIn[1]
$setServerVar[msgid;$sendEmbedMessage[$findChannel[$getServerVar[ticketnumber]-ticket];<@$authorID>;__Your Ticket__;;__Reason:__
$input[reason]

Please wait until a staff member responds;1700fb;;;;;$serverIcon[$guildID];;yes;yes]]

$useChannel[$findChannel[$getServerVar[ticketnumber]-ticket]]

$addButton[no;claimticket-$authorID;Claim Ticket;primary;no;;$getServerVar[msgid]]
$addButton[no;closeticket-$findChannel[$getServerVar[ticketnumber]-ticket];Close Ticket;danger;no;;$getServerVar[msgid]]

$endif

===============================
Code 3) $onInteraction
BDScript2
**********************
$onlyIf[$checkContains[$customID;claimticket-;closeticket-]==true;]
$nomention

$if[$checkContains[$customID;claimticket-]==true]
$textSplit[$customID;-]
$onlyAdmin[Only admins]
$editChannelPerms[$channelID;$findRole[@everyone];-sendmessages;-readmessagehistory;-readmessages;-attachfiles]

$editChannelPerms[$channelID;$splitText[2];+sendmessages;+readmessagehistory;+readmessages;+attachfiles]

$editChannelPerms[$channelID;$authorID;+sendmessages;+readmessagehistory;+readmessages;+attachfiles]

$sendMessage[$nickname has claimed this ticket]
$endif

$if[$checkContains[$customID;closeticket-]==true]
$textSplit[$customID;-]

$sendMessage[This ticket will close in <t:$calculate[$getTimestamp+60]:R>]

$async[1]
$replyIn[60]
$deleteChannels[$splitText[2]]
$endasync
$endif
 